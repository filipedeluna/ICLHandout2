PARSER_BEGIN(Parser)
package parser;

import interpreter.Interpreter;
import interpreter.errors.*;
import typechecker.TypeChecker;
import typechecker.errors.*;
//import compiler.Compiler;
// import compiler.errors.*;

import node.*;
import node.block.*;
import node.types.*;
import node.variable.*;
import node.relational.*;
import node.logical.*;
import node.arithmetic.*;
import node.instruction.*;
import types.*;
import values.*;

import java.util.ArrayList;

/** ID lister. */
public class Parser {
    /** Main entry point. */
    public static void main(String args[]) {
        parser.Parser parser = new parser.Parser(System.in);

        ASTNode exp;
        Compiler compiler = null;
        Interpreter interpreter;
        TypeChecker typeChecker;

        while (true) {
            try {
                exp = parser.Start();

                // PARSE
                interpreter = new Interpreter();
                typeChecker = new TypeChecker();

                if (exp != null) {
                    exp.typeCheck(typeChecker);

                     IValue value = exp.eval(interpreter);
                     if (value != null)
                       System.out.println("Result: " + value.asString());
                } else
                    System.out.println("Result: null");

                System.out.println();

                // COMPILE
                compiler = new Compiler();
                exp.compile(compiler);
                compiler.end();
            } catch (Exception e) {
                handleErrors(e, compiler);
                parser.ReInit(System.in);
            }
        }
    }

    /*
        UTILS
    */
    private static void handleErrors(Exception e, Compiler compiler) {
        try {
          if (compiler != null)
            compiler.deleteGeneratedFiles();
        } catch (CompileError ce) {
            System.err.println(ce.getMessage());
        }

        if (e instanceof CompileError || e instanceof InterpretationError || e instanceof TypeCheckError)
            System.err.println(e.getMessage());
        else
            e.printStackTrace();
    }
}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\r"
| "\n"
}

TOKEN :
{
  < Num: (["0"-"9"]) + >
  | < ADD : "+" >
  | < SUB : "-">
  | < MUL : "*">
  | < DIV : "/">
  | < LPAR : "(" >
  | < RPAR : ")" >

  // Boolean
  | < BOOL : "true" | "false" >

  // Decl
  | < EQ : "=" >
  | < LET : "let" >
  | < IN : "in" >
  | < NEW : "new" >
  | < END : "end" >

  // Blocks
  | < WHILE : "while" >
  | < DO : "do" >
  | < IF : "if" >
  | < THEN : "then" >
  | < ELSE : "else" >

  // Symbols
  | < PROGRAM_END : ";;" >
  | < SEQ_END : ";" >
  | < DEREF : "!" >
  | < APPLY : ":=" >

  // Relational
  | < EQEQ : "==" >
  | < DIFFERS : "<>" >
  | < GREATER : ">" >
  | < LOWER : "<" >
  | < GREATER_EQ : ">=" >
  | < LOWER_EQ : "<=" >

  // Logical
  | < AND : "&&" >
  | < OR : "||" >
  | < COMP : "~" >

  // Functions
  | < FUNC : "fun" >
  | < FUNCSTART : "->">
  | < COLON : ":">
  | < COMMA : ",">

  // Types
  | < TYPEINT : "int" >
  | < TYPEBOOL : "bool" >
  | < TYPESTRUCT : "struct" >
  | < TYPESTRING : "string" >
  | < REFTYPE : "ref">

  // Structures
  | < FIELD : "." >
  | < LBRKT : "{" >
  | < RBRKT : "}" >

  // STRINGS
  | < QUOTE : "\"" >

  // Commands
  | < PRINT : "println" >

  //-------
  | < Id: ["a"-"z","A"-"Z"] ( ["a"-"z","A"-"Z","0"-"9"] )* >
}

ASTNode Start():
{
  ASTNode n = null;
}
{
   ( n = Seq() ) ? <PROGRAM_END>
   { return n; }
}

ASTNode Seq() :
{
    ASTNode n1, n2;
}
{
    n1 = Log() ( <SEQ_END> n2 = Seq() { n1 = new ASTSeq(n1, n2); } ) ?

    { return n1; }
}

ASTNode Log() :
{
  Token op;
  ASTNode n1, n2;
}
{
    n1 = Exp()

    (
        (
             op = <EQEQ> | op = <DIFFERS>
             | op = <GREATER> | op = <LOWER>
             | op = <GREATER_EQ> | op = <LOWER_EQ>
             | op = <AND> | op = <OR>
        )

        n2 = Exp()

        {
            if (op.kind == EQEQ)
                n1 = new ASTRelational(RelationalOperation.EQUALS , n1, n2);
            if (op.kind == DIFFERS)
                n1 = new ASTRelational(RelationalOperation.DIFFERS , n1, n2);
            if (op.kind == GREATER)
                n1 = new ASTRelational(RelationalOperation.GREATER_THAN , n1, n2);
            if (op.kind == LOWER)
                n1 = new ASTRelational(RelationalOperation.LOWER_THAN , n1, n2);
            if (op.kind == GREATER_EQ)
                n1 = new ASTRelational(RelationalOperation.GREATER_OR_EQUALS , n1, n2);
            if (op.kind == LOWER_EQ)
                n1 = new ASTRelational(RelationalOperation.LOWER_OR_EQUALS , n1, n2);
            if (op.kind == AND)
                n1 = new ASTLogical(LogicalOperation.AND, n1, n2);
            if (op.kind == OR)
                n1 = new ASTLogical(LogicalOperation.OR, n1, n2);
        }
    ) *

    { return n1; }
}

ASTNode Exp() :
{
  Token op;
  ASTNode n1, n2;
}
{
    n1 = Term()

    (
        ( op = <ADD> | op = <SUB> )

        n2 = Term()

        {
            if (op.kind == ADD)
                n1 = new ASTArithmetic(ArithmeticOperation.ADD, n1, n2);
            if (op.kind == SUB)
                n1 = new ASTArithmetic(ArithmeticOperation.SUB, n1, n2);
        }
    ) *

    { return n1; }
}

ASTNode Term() :
{
    Token op;
    ASTNode n1, n2;
}
{
  n1 = Fact()

  (
    ( op = <MUL> | op = <DIV> )

    n2 = Term()
    {
        if (op.kind == MUL)
          n1 = new ASTArithmetic(ArithmeticOperation.MUL, n1, n2);
        if (op.kind == DIV)
          n1 = new ASTArithmetic(ArithmeticOperation.DIV, n1, n2);
    }
  ) ?
  { return n1; }
}

ASTNode Fact() :
{
    Token t;
    ASTNode n1, n2, n3;
}
{
    (
        n1 = Init()
        | <SUB> n1 = Log() { n1 = new ASTNeg(n1); }
        | <COMP> n1 = Log() { n1 = new ASTComp(n1); }
        | <LPAR> n1 = Val() <RPAR> ( <APPLY> n2 = Log() { n1 = new ASTApply(n1, n2); } ) ?
        | <LET> n1 = Decl() <END>
        | <WHILE> n1 = Log() <DO> n2 = Seq() <END> { n1 = new ASTWhile(n1, n2); }
        | <IF> n1 = Log() <THEN> n2 = Seq() <ELSE> n3 = Seq() <END> { n1 = new ASTIf(n1, n2, n3); }
        | <PRINT> n1 = Log() { n1 = new ASTPrint(n1); }
        | n1 = Val()
    )

    { return n1; }
}

ASTNode Val() :
{
    Token t1, t2;
    ASTNode n1, n2;
    ArrayList<ASTNode> funParams;
}
{
    ( n1 = Lit() { return n1; } )
    | <DEREF> t1 = <Id> ( <FIELD> t2 = <Id>  { return new ASTStructFieldDeref(t1.image, t2.image); } )? { return new ASTDeref(t1.image); }
    | <LBRKT> n1 = Struct() <RBRKT> { return n1; }
    | t1 = <Id> { n1 = new ASTVar(t1.image); } ( <FIELD> t2 = <Id> { n1 = new ASTStructFieldVar(t1.image, t2.image); } )?
        (
            <APPLY> n2 = Log() { return new ASTApply(n1, n2); }
            | <LPAR>
                    { funParams = new ArrayList<ASTNode>(); }
                    (
                        <RPAR> { return new ASTFunCall(t1.image); }
                        | n2 = Log() { funParams.add(n2); } ( <COMMA> n2 = Log() { funParams.add(n2); } )*
                        <RPAR> { return new ASTFunCall(t1.image, funParams); }
                    )
        )
    { return n1; }
}

ASTNode Lit() :
{
    Token t;
    String s = new String();
}
{
    t = <Num> { return new ASTInt(new VInt(Integer.parseInt(t.image))); }
    | t = <BOOL> { return new ASTBool(new VBool(Boolean.parseBoolean(t.image))); }
    | <QUOTE> ( t = <Id> { s = t.image; } ( t = <Id> { s += " " + t.image; } )* )?  <QUOTE>
        { return new ASTString(new VString(s)); }
}

ASTNode Decl() :
{
  Token t;
  IType type;
  ASTNode n;
  ArrayList<ASTAssign> assignments;
}
{
    { assignments = new ArrayList<ASTAssign>(); }

    (
        t = <Id> <COLON> (
            ( <REFTYPE> type = Singleton() <EQ> n = Init() )
            | type = FunType() <EQ> n = Init()
        )
        { assignments.add(new ASTAssign(t.image, type, n)); }
    )*
    <IN> n = Seq()

    { return new ASTLet(n, assignments); }
}

IType FunType() :
{
    IType t;
    ArrayList<IType> paramTypes;
}
{
   { paramTypes = new ArrayList<IType>(); }

   <LPAR> ( t = Singleton() { paramTypes.add(t); } ( <COMMA> t = Singleton() { paramTypes.add(t); } )* )?
   <RPAR> ( t = Singleton() { return new TFun(paramTypes, t); } | { return new TFun(paramTypes); } )
}

ASTNode Init() :
{
    Token t;
    ASTNode n;
    ArrayList<FunParam> funParams;
}
{
    <NEW> (
        n = LitInit() { return n; }
        | <LBRKT> n = Struct() <RBRKT> { return new node.initialization.ASTStructInit(n); }
    )
    | <FUNC> funParams = FunParams() <FUNCSTART> n = Log() <END> { return new ASTFunInit(funParams, n); }
}

ASTNode LitInit() :
{
    Token t;
    String s = new String();
}
{
    t = <Num> { return new ASTInit(new VInt(Integer.parseInt(t.image))); }
    | t = <BOOL> { return new ASTInit(new VBool(Boolean.parseBoolean(t.image))); }
    | t = <Id> { return new ASTVarInit(t.image); }
    | <QUOTE> ( t = <Id> { s = "" + t.image; } ( t = <Id> { s += " " + t.image; } )* )? <QUOTE>
        { return new ASTInit(new VString(s)); }
}

ASTStruct Struct() :
{
    Token t;
    ASTNode n;
    ArrayList<ASTStructParam> structParams;
}
{
   { structParams = new ArrayList<ASTStructParam>(); }
   ( t = <Id> <EQ> n = Lit() { structParams.add(new ASTStructParam(t.image, n)); }
        ( <SEQ_END> t = <Id> <EQ> n = Lit() { structParams.add(new ASTStructParam(t.image, n)); } )*
   )?
   { return new ASTStruct(structParams); }
}

ArrayList<FunParam> FunParams() :
{
  Token t;
  IType type;
  ArrayList<FunParam> funParams;
}
{
    { funParams = new ArrayList<FunParam>(); }

    (
       t = <Id> <COLON> type = Singleton() { funParams.add(new FunParam(t.image, type)); }
       ( <COMMA> t = <Id> <COLON> type = Singleton() { funParams.add(new FunParam(t.image, type)); } )*
    )?

    { return funParams; }
}

IType Singleton() :
{
}
{
    <TYPEINT> { return TInt.SINGLETON; }
   | <TYPEBOOL> { return TBool.SINGLETON; }
   | <TYPESTRUCT> { return TStruct.SINGLETON; }
   | <TYPESTRING> { return TString.SINGLETON; }
}
